<jittershader name="bs.portal.tex.proj.multi.direction.6-beam.jxs">
	<description>SPARCK - multi projection - Shader</description>
	<param name="mode" type="int" default="0" />
	<param name="stage_mode" type="int" default="0" />
	<param name="projection_mode" type="float" default="0" />
	
	<param name="beamer_enable" type="float" default="1. 1. 1. 1. 1. 1." />
	<param name="beamer_count" type="int" default="0" />

	<param name="beamer_color" type="vec4" default="1. 0. 0. 1. 0. 1. 0. 1. 0. 0. 1. 1. 0. 1. 1. 1. 1. 0. 1. 1. 1. 1. 0. 1." />
	<param name="offColor" type="vec4" default="0. 0. 0. 1." />

 	<param name="beamer_pos" type="vec3" default="0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0." />
	<param name="beamer_dir" type="vec3" default="0. 0. 1. 0. 0. 1. 0. 0. 1. 0. 0. 1. 0. 0. 1. 0. 0. 1." />
	<param name="beamer_up" type="vec3" default="1. 0. 0. 1. 0. 0. 1. 0. 0. 1. 0. 0. 1. 0. 0. 1. 0. 0." />

	<param name="beamer_m_matrix" type="mat4" default="1. 0. 0. 0. 0. 1. 0. 0. 0. 0. 1. 0. 0. 0. 0. 1." />
	<param name="beamer_v_matrix" type="mat4" default="0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0." />
	<param name="beamer_p_matrix" type="mat4" default="0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0. 0." />
	<param name="world_matrix" type="mat4" state="WORLD_MATRIX" />
	<param name="viewport_matrix" type="mat4" default="0.5 0. 0. 0. 0. 0.5 0. 0. 0. 0. 0.5 0. 0.5 0.5 0.5 0." />

	<param name="bevel_size" type="float" default="0. 0. 0. 0. 0. 0." />
	<param name="bevel_curve" type="float" default="1. 1. 1. 1. 1. 1." />
	<param name="bevel_round" type="int" default="0 0 0 0 0 0" />

	<param name="angle_mode" type="float" default="0.0" />

	<param name="angle_limit" type="float" default="0. 0. 0. 0. 0. 0." />
	<param name="angle_falloff" type="float" default="0.3 0.3 0.3 0.3 0.3 0.3" />
	<param name="angle_limit_low" type="float" default="0. 0. 0. 0. 0. 0." />
	<param name="angle_limit_high" type="float" default="0.2 0.2 0.2 0.2 0.2 0.2" />

	<param name="far_clip" type="float" default="0. 0. 0. 0. 0. 0." />

	<param name="blend_spread" type="float" default="1.0" />
	<param name="back_blend" type="float" default="1." />
	<param name="distance_influence" type="float" default="1." />

	<param name="interpolation_correction" type="float" default="0.1" />

	<param name="collada_factor" type="mat4" default="1. 0. 0. 0. 0. 1. 0. 0. 0. 0. 1. 0. 0. 0. 0. 1." />

	<param name="tex0" type="int" default="0" />
	<param name="tex1" type="int" default="1" />
	<param name="tex2" type="int" default="2" />
	<param name="tex3" type="int" default="3" />
	<param name="tex4" type="int" default="4" />
	<param name="tex5" type="int" default="5" />

	<language name="glsl" version="1.2">
		<bind param="tex0" program="fp" />
		<bind param="tex1" program="fp" />
		<bind param="tex2" program="fp" />
		<bind param="tex3" program="fp" />
		<bind param="tex4" program="fp" />
		<bind param="tex5" program="fp" />

		<bind param="beamer_count" program="fp" />
		<bind param="beamer_color" program="fp" />

		<bind param="bevel_size" program="fp" />
		<bind param="bevel_curve" program="fp" />
		<bind param="bevel_round" program="fp" />

		<bind param="beamer_pos" program="fp" />
		<bind param="beamer_dir" program="fp" />

		<bind param="beamer_v_matrix" program="vp" />
		<bind param="beamer_p_matrix" program="vp" />

		<bind param="projection_mode" program="fp" />
		<bind param="stage_mode" program="fp" />
		<bind param="mode" program="fp" />

		<bind param="blend_spread" program="fp" />
		<bind param="back_blend" program="fp" />
		<bind param="distance_influence" program="fp" />
		<bind param="angle_mode" program="fp" />

		<bind param="angle_limit_low" program="fp" />
		<bind param="angle_limit_high" program="fp" />

		<bind param="far_clip" program="fp" />

		<bind param="offColor" program="fp" />

		<bind param="world_matrix" program="vp" />
		<bind param="collada_factor" program="vp" />
		<bind param="interpolation_correction" program="vp" />
		<bind param="viewport_matrix" program="vp" />

		<program name="vp" type="vertex" source="bs.portal.pass.projectory.1.vs.gl2.glsl" />

		<program name="fp" type="fragment">
<![CDATA[

// MIT License
// Martin Froehlich for tecartlab.com
// Copyright 2020 - tecartlab.com

#version 120
#extension GL_ARB_texture_rectangle : enable

/*
	Multifunctional texture projection shader with full feature set.
*/

uniform float projection_mode;
uniform int stage_mode;
uniform int mode;

// samplers
uniform sampler2DRect tex0;
uniform sampler2DRect tex1;
uniform sampler2DRect tex2;
uniform sampler2DRect tex3;
uniform sampler2DRect tex4;
uniform sampler2DRect tex5;

uniform int beamer_count;
uniform vec4 beamer_color[6];

uniform mat4 beamer_v_matrix[6];

uniform vec3 beamer_pos[6];
uniform vec3 beamer_dir[6];

uniform float bevel_size[6];
uniform float bevel_curve[6];
uniform int bevel_round[6];

uniform vec4 offColor; 	// off color

uniform float distance_influence;
uniform float back_blend;
uniform float blend_spread;
uniform float angle_mode;

uniform float angle_limit_low[6];
uniform float angle_limit_high[6];

uniform float far_clip[6];

uniform float interpolation_correction;

// generated by the vertex shader

varying vec4 beamer_uv[1];		// beamer uv position
varying vec2 beamer_texcoord[1];// beamer texcoord

varying vec3 normal;	// surface normal
varying vec3 worldPos;	// vertex world position


// constants

const float PI = 3.1415926535897932384626433832795;
const float PI_HALF = PI / 2.0;
const vec4 WHITE = vec4( 1.0, 1.0, 1.0, 1.0);
const vec4 BLACK = vec4( 0.0, 0.0, 0.0, 1.0);

// entry point
void main()
{
    // calculate rotation matrix from beamer view matrix - its not accurate and cause problem if
    // the matrix is not scale = 1 1 1, but its a quick and functional fix that should not be a heavy
    // performance hit.
    mat4 rot_matrix = beamer_v_matrix[0];
    rot_matrix[3] = vec4(0., 0., 0., 1.); // the positional infos are removed.

    // calculate the viewray from the camera to this fragment
    vec4 ray = vec4(worldPos - beamer_pos[0], 1);
    vec3 viewline = (rot_matrix * ray).xyz;
    vec3 vl_abs = abs(viewline);
    
	vec3 raynormal = normalize(-ray.xyz);

	// calculate the angle between the surface normal and the projected beam.
	// 		rsp. the angle between the surface normal and the projector direction.
	float angle = dot(normal, raynormal);

	// sets from which side (back, both, front) it is visible
	float side = (projection_mode == 0.)? 1.:max(sign(angle * projection_mode), 0.);

	vec2 vpSize = vec2(0.25, 0.33333);
	vec4 texcoord0;

	if(vl_abs.x > vl_abs.y && vl_abs.x > vl_abs.z){
		if(viewline.x >= 0.0){ //RIGHT
			texcoord0 = vec4(
						(viewline.z / vl_abs.x) * (vpSize.s / 2.0) + (vpSize.s * 2.5),
						(viewline.y / vl_abs.x) * (vpSize.t / 2.0) + (vpSize.t * 1.5), 0., 1.);
		}else{ //LEFT
			texcoord0 = vec4(
						-(viewline.z / vl_abs.x) * (vpSize.s / 2.0) + (vpSize.s * 0.5),
						(viewline.y / vl_abs.x) * (vpSize.t / 2.0) + (vpSize.t * 1.5), 0., 1.);
		}
	} else 	if(vl_abs.y > vl_abs.x && vl_abs.y > vl_abs.z){
		if(viewline.y >= 0.0){ //TOP
			texcoord0 = vec4(
						(viewline.x / vl_abs.y) * (vpSize.s / 2.0) + (vpSize.s * 1.5),
						(viewline.z / vl_abs.y) * (vpSize.t / 2.0) + (vpSize.t * 2.5), 0., 1.);
		}else{
			texcoord0 = vec4( //BOTTOM
						(viewline.x / vl_abs.y) * (vpSize.s / 2.0) + (vpSize.s * 1.5),
						-(viewline.z / vl_abs.y) * (vpSize.t / 2.0) + (vpSize.t * 0.5), 0., 1.);
		}
	} else 	if(vl_abs.z > vl_abs.x && vl_abs.z > vl_abs.y){
		if(viewline.z >= 0.0){
			texcoord0 = vec4( //BACK
						-(viewline.x / vl_abs.z) * (vpSize.s / 2.0) + (vpSize.s * 3.5),
						(viewline.y / vl_abs.z) * (vpSize.t / 2.0) + (vpSize.t * 1.5), 0., 1.);
		}else{
			texcoord0 = vec4( //FRONT
						(viewline.x / vl_abs.z) * (vpSize.s / 2.0) + (vpSize.s * 1.5),
						(viewline.y / vl_abs.z) * (vpSize.t / 2.0) + (vpSize.t * 1.5), 0., 1.);
		}
	}

	gl_FragColor = (side > 0)?texture2DRect(tex0, (gl_TextureMatrix[0] * texcoord0).st):offColor;
    
    // in case it is rendered to stage and stage_mode is set to colored:
	if(mode == 0 && stage_mode >= 1){
		if(vl_abs.x > vl_abs.y && vl_abs.x > vl_abs.z){
			gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
		} else 	if(vl_abs.y > vl_abs.x && vl_abs.y > vl_abs.z){
			gl_FragColor = vec4(0.0, 1.0, 0.0, 1.0);
		} else 	if(vl_abs.z > vl_abs.x && vl_abs.z > vl_abs.y){
			gl_FragColor = vec4(0.0, 0.0, 1.0, 1.0);
		}
	}
}

]]>
		</program>
	</language>
</jittershader>
